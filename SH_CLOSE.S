;
;**************************************************************
;
;                CLOSE MODULE
;                30/3/83
;
;**************************************************************
;
;	GLOBAL		DELTMP,CL_STR
;	EXTERNAL	DRVSEL,DELIX
;	EXTERNAL	END1,OUT_T2,NTCLSE
;	EXTERNAL	CLOSEN,CLOSIX

	ORG	1708H
;


	INC	HL				; REPEAT THE BASE ROM INSTRUCTION
	RST	INSER_				; JUST IN CASE FIRST CALL TO SHADOW ROM

	SRL	A				; 'A' HOLDS THE ALTERED STREAM NUMBER
	SUB	03H				; FIND STREAM NUMBER
						; USING	(A/2-3)
	RES	1,(IY+FLAGS3-Y)			; TO TELL CL_STR TO CLOSE PROPERLY
	CALL	CL_STR				; CLOSE THE STREAM NOW

	JP	END1



;______________________________________________________________________

CL_STR		; CLOSE STREAM 'A'
	RST	CALBAS
	DEFW	1727H				; FIND 'OFFSET' FOR THAT STREAM
						; BC - OFFSET
						; HL - STR_PTR
	LD	A,C
	OR	B
	RET	Z				; RETURN 'Z' IS STREAM ALREADY CLOSED

	PUSH	BC				; STACK  - OFFSET
	PUSH	HL				; STACK  - OFFSET, STR_PTR
	LD	HL,(CHANS_)			; BASE OF CHANNEL AREA
	DEC	HL
	ADD	HL,BC				; BASE OF STREAM A'S CHANNEL DATA
	EX	(SP),HL				; STACK  - OFFSET, CH_PTR
						; HL - STR_PTR AGAIN

	RST	CALBAS
	DEFW	16EBH				; TO CLOSE THE STREAM / RESET IT

	LD	HL,(CHANS_)			; BASE OF CHANNEL AREA
	LD	DE,0014H
	ADD	HL,DE				; ADDRESS AFTER K, S, R & P CHANNEL DATA BLOCKS
	POP	DE				; FETCH CH_PTR
	SCF					; DECREMENTED SUBTRACTION
	SBC	HL,DE
	POP	BC				; FETCH OFFSET
	RET	NC				; RETURN NOW IF THERE IS NO 'RECLAIMING' TO BE DONE

	PUSH	BC				; STACK  - OFFSET
	PUSH	DE				; STACK  - OFFSET, CH_PTR

	EX	DE,HL				; CH_PTR TO HL
	LD	(CURCHL),HL			; MAKE THIS CHANNEL 'CURRENT'
	INC	HL
	INC	HL
	INC	HL
	INC	HL
	LD	A,(HL)				; FIND THE 'LETTER'
	LD	DE,0005H			; THE LENGTH IS 5 LOCATIONS FURTHER ON
	ADD	HL,DE
	LD	E,(HL)				; FIND THE 'LOW LENGTH'
	INC	HL
	LD	D,(HL)				; AND THE 'HIGH LENGTH'
	PUSH	DE				; STACK  - OFFSET, CHR_PTR, LENGTH

	CP	42H				; IS IT 'B' ?
	JR	Z,CL_BT
	CP	54H				; IS IT 'T' ?
	JR	NZ,CL_N

CL_BT
	BIT	1,(IY+FLAGS3-Y)			; JUST DELETE IF CLEAR#
	JR	NZ,CL_R
	LD	A,0DH				; A CARRIAGE RETURN CHARACTER
	CALL	OUT_T2
	JR	CL_R

CL_N
	CP	4EH				; IS THE LETTER A 'N' ?
	JR	NZ,CL_M
	BIT	1,(IY+FLAGS3-Y)			; JUST DELETE IF CLEAR#
	JR	NZ,CL_R
	CALL	NTCLSE
	JR	CL_R

CL_M	CP	4DH				; IS THE LETTER A 'M' ?
	JR	NZ,CL_R				; DO THIS FOR THE SAKE OF FUTURE DEVICES
	POP	DE
	POP	IX
	POP	DE
	BIT	1,(IY+FLAGS3-Y)
	JP	Z,CLOSIX			; CLOSE
	JP	DELIX				; CLEAR#

		; NOW RECLAIM THE CHANNEL DATA BYTES FOR THIS STREAM
CL_R	POP	BC				; FETCH THE 'LENGTH'
	POP	HL				; FETCH THE 'CH_PTR'
	PUSH	BC				; STACK  - OFFSET, LENGTH
	RST	CALBAS
	DEFW	19E8H				; RECLAIM THE BYTES

		; NOW CONSIDER ALL THE STREAM DATA BYTES
		; REDUCING THOSE THAT NOW POINT TOO FAR

	XOR	A				; CONSIDER THE OFFSETS FOR STREAMS 00H TO 0FH
	LD	HL,5C16H			; BASE ADDRESS FOR STREAM 00H

CL_S1	LD	E,(HL)				; FETCH THE 'OFFSET' FOR THE STREAM
	INC	HL
	LD	D,(HL)
	DEC	HL
	LD	(X_PTR),HL			; STORE THE POINTER TEMPORARILY
	POP	BC				; THE 'LENGTH'
	POP	HL				; THE 'OFFSET'
	PUSH	HL
	PUSH	BC				; RESTORE THEM FOR THE NEXT LOOP
	AND	A
	SBC	HL,DE				; TEST OLD OFFSET AGAINST NEW OFFSET
	JR	NC,CL_S2			; JUMP UNLESS DATA BYTES NEED REDUCING

	EX	DE,HL				; NEW OFFSET TO HL
	AND	A
	SBC	HL,BC				; REDUCE OFFSET BY 'LENGTH'
	EX	DE,HL				; MOVE TO DE
	LD	HL,(X_PTR)
	LD	(HL),E
	INC	HL
	LD	(HL),D				; ENTER THE NEW VALUE

CL_S2   LD	HL,(X_PTR)
	INC	HL
	INC	HL				; STEP ON
	INC	A
	CP	10H
	JR	C,CL_S1

	LD	(IY+X_PTR+1-Y),00H		; RESET X_PTR
	POP	HL
	POP	HL
	RES	1,(IY+FLAGS3-Y)			; TIDY UP AFTERWARDS
	RET




;
; DELTMP DELETES ALL TEMPORARY BUFFERS
; IE THOSE MARKED BY BIT 7 SET IN THE TYPE
;
DELTMP
	LD	IX,(CHANS)
	LD	DE,20
	ADD	IX,DE
DT1
	LD	A,(IX+0)			; LAST CHANNEL?
	CP	80H
	JR	NZ,DT15
	LD	A,CTS.AND.SELD			; EVERYTHING OFF
	OUT	(PORTC),A
	XOR	A				; INCLUDING MOTORS
	JP	DRVSEL
	RET
;
DT15
	LD	A,(IX+CHTYPE)			; TEMPORARY MICRODRIVE?
	CP	'M'+80H
	JR	NZ,DT2
	CALL	DELIX				; YES DELETE IT
	JR	DELTMP				; BACK FOR MORE
DT2
	CP	'N'+80H				; TEMPORARY NET
	JR	NZ,DT3
;
	LD      BC,N_LEN
	PUSH	IX
	POP	HL
	RST	CALBAS
	DEFW	19E8H
	JR	DELTMP
DT3
	LD	E,(IX+CHLEN)
	LD	D,(IX+CHLEN+1)
	ADD	IX,DE
	JR	DT1				; NEXT CHANNEL
;
