;
;**************************************************************
;
;                    SYNTAX/INTERPRETER MODULE
;
;**************************************************************

;	GLOBAL S_CAT
;	GLOBAL S_FORM
;	GLOBAL S_MOVE
;	GLOBAL S_ERAS
;	GLOBAL S_OPEN
;	GLOBAL END1,ST_END,N_CHEK,N_NAME
;	GLOBAL LETT,END_ER,COMM,MNCHEK
;	GLOBAL S_CLS
;	GLOBAL S_CLR
;	EXTERNAL RETBAS
;	EXTERNAL ACT_FT, ACT_OT
;	EXTERNAL ACT_FM, ACT_OM
;	EXTERNAL ACT_ON
;	EXTERNAL ACT_CA, ACT_ER, ACT_MO
;	EXTERNAL	CL_STR


;______________________________________________________________________

S_CAT		; CONSIDER A 'CAT' STATEMENT
	LD	HL,DR+S_STR1			; STREAM SYS. VARS.
	LD	(HL),02H			; BY DEFAULT STREAM 02H
	RST	CALBAS
	DEFW	0020H				; STEP ON
	CP	0DH				; END OF LINE
	JR	Z,S_CAT1
	CP	3AH				; END OF STATEMENT?
S_CAT1	JP	Z,MDN_ER			; 'MISSING DRIVE NUMBER'  (RUN-TIME ERROR ONLY)
	CP	23H				; HASH?
	JR	NZ,S_CAT2
	CALL	N_STR				; FETCH THE 'NEXT' STREAM NUMBER	
	CALL	COMM				; INSIST ON A SEPARATOR
	JR	NZ,END_ER
	RST	CALBAS
	DEFW	0020H				; STEP ON
S_CAT2	CALL	NUMB_				; FETCH A NUMBER
	CALL	ST_END				; INSIST THE STATEMENT FINISHES
						; CAT RUN-TIME
	CALL	DR_CHK				; CHECK IT IS IN RANGE FOR A DRIVE NUMBER
	JP	ACT_CA				; ACT ON THE 'CAT' STATEMENT
;______________________________________________________________________

END_ER	RST	SH_ERR
	DEFB	00H				; 'NONSENSE IN BASIC'
;______________________________________________________________________

S_FORM		; CONSIDER A 'FORMAT' STATEMENT
	CALL	N_LETT				; FIND THE 'NEXT' LETTER & A NUMBER IF PRESENT
	CALL	COMM				; LOOK FOR A SEPARATOR
        JR	NZ,S_FOR1			; JUMP IF NO MORE
	CALL	N_NAME				; FIND THE 'NEXT' NAME
S_FOR1	CALL	ST_END				; INSIST THE STATEMENT ENDS

		; FORMAT RUN_TIME
	LD	A,(DR+L_STR1)			; FETCH THE LETTER
	CP	54H				; IS IT 'T' ?
	JR	Z,S_FOR2
	CP	42H				; IS IT 'B' ?
	JR	NZ,S_FOR3
S_FOR2	CALL	B_CHEK				; CHECK THE BAUD RATE SPECIFIED
	JP	ACT_FT				; ACT ON ' FORMAT "T" ' & ' FORMAT "B" '


S_FOR3	CP	4EH				; IS IT 'N' ?
	JR	NZ,S_FOR4
	CALL	N_CHEK				; CHECK THE STATION NUMBER
	LD	A,(DR+D_STR1)			; FETCH NET NUMBER
	AND	A
	JP	Z,ISN_ER			; DO NOT ALLOW - FORMAT "N",0
	LD	(DR+NTSTAT),A			; GIVE OWN STATION A NEW NUMBER
	JP	END1

S_FOR4	CALL	MNCHEK				; IS IT ' "M",DRIVE NUMBER,FILENAME' ?
	JP	ACT_FM				; ACT ON ' FORMAT "M",DRIVE NUMBER, FILENAME '

;______________________________________________________________________




S_OPEN		; CONSIDER AN 'OPEN' STATEMENT
	CALL	N_STR				; FIND THE 'NEXT' STREAM NUMBER
	CALL	COMM				; INSIST ON THE SEPARATOR
	JR	NZ,END_ER
	CALL	N_LETT				; FIND THE 'NEXT' LETTER & NUMBER (IF PRESENT)
	CALL	COMM				; IS THERE A SEPARATOR?
	JR	NZ,S_OP1
	CALL	N_NAME				; FIND THE 'NEXT' NAME
S_OP1	CALL	ST_END				; INSIST THE STATEMENT ENDS

		; OPEN RUN-TIME
	LD	A,(DR+S_STR1)			; FETCH STREAM NUMBER
	RST	CALBAS
	DEFW	1727H				; FETCH STREAM OFFSET
	LD	HL,0011H			; CLOSED STREAMS HAVE 0000H
			; PERMANENT STREAMS HAVE 0000H - 0010H
	AND	A
	SBC	HL,BC				; TEST IF CLOSED BUT NOT A PERMANENT
	JR	C,SAO_ER
	LD	A,(DR+L_STR1)			; FETCH THE LETTER
	CP	54H				; IS IT 'T' ?
	JR	Z,S_OP2
	CP	42H				; IS IT 'B' ?
        JR	NZ,S_OP3
S_OP2	JP	ACT_OT				; ACT ON ' OPEN STREAM,"T" ' & ' OPEN STREAM,"B" '
S_OP3	CP	4EH				; IS IT 'N' ?
	JR	NZ,S_OP4
	CALL	N_CHEK				; CHECK THE STATION NUMBER
	JP	ACT_ON				; ACT ON ' OPEN STREAM,"N",STATION NUMBER '
S_OP4	CALL	MNCHEK				; IS IT ' "M",DRIVE NUMBER,FILENAME '?
	JP	ACT_OM				; ACT ON ' OPEN STREAM,"M",DRIVE NUMBER,FILENAME

SAO_ER	RST	SH_ERR
	DEFB	0BH				; 'STREAM ALREADY OPEN'
;______________________________________________________________________




S_ERAS		; CONSIDER AN 'ERASE' STATEMENT
	CALL	NLETNN				; FIND 'LETTER, NUMBER & NAME '
	CALL	ST_END				; INSIST THE STATEMENT ENDS

		; ERASE RUN-TIME
	CALL	MNCHEK				; IS IT 'M',DRIVE NUMBER,NAME
	JP	ACT_ER				; ACT ON ' ERASE "M",DRIVE NUMBER,FILENAME '

;______________________________________________________________________



S_MOVE		; CONSIDER A MOVE STATEMENT
	CALL	ST_V_F				; STREAM OR FILE?
	CALL	SV_1_2				; SWITCH OVER THE SETS
	RST	CALBAS
	DEFW	0018H				; FETCH PRESENT CHARACTER
	CP	0CCH				; IS IS 'TO' ?
	JR	NZ,SC_ERR
	CALL	ST_V_F				; AGAIN STREAM OR FILE?
	CALL	SV_1_2				; SWITCH BACK THE SETS
	RST	CALBAS
	DEFW	0018H				; GET PRESENT CHARACTER
	CALL	ST_END				; INSIST STATEMENT ENDS

		; MOVE RUN-TIME
	JP	ACT_MO

;______________________________________________________________________



S_CLS		; GIVES DEFAULT SCREEN
	RST	CALBAS
	DEFW	0020H				; STEP ON
	CP	23H				; 'HASH' CHARACTER
	JR	NZ,SC_ERR
	RST	CALBAS
	DEFW	0020H				; STEP ON PAST 'HASH'
	CALL	ST_END				; STATEMENT MUST END HERE

		; CLS C RUN-TIME
	LD	HL,0038H
	LD	(ATTR_P),HL			; SET PERMANENTS
	LD	(ATTR_T),HL			; SET TEMPORARIES
	LD	(IY+BORDCR-Y),L			; SET BORDER
	LD	(IY+P_FLAG-Y),H			; CLEAR ALL FLAGS IN P_FLAG
	LD	A,07H
	OUT	(0FEH),A			; BORDER IS MADE WHITE BEFORE THE SCREEN IS CLEARED
	RST	CALBAS
	DEFW	0D6BH				; CLEAR THE SCREEN GIVING THE ORIGINAL COLOURS
        JP	END1


;______________________________________________________________________


S_CLR		; CLEAR ALL STREAMS
	RST	CALBAS 
	DEFW	0020H				; STEP ON
	CP	23H				; HASH?
SC_ERR	JP	NZ,END_ER
	RST	CALBAS
	DEFW	0020H				; STEP PAST THE HASH
	CALL	ST_END				; STATEMENT MUST END HERE


		; CLEAR # RUN-TIME
						; CLOSE ALL STREAMS
	XOR	A				; START WITH STREAM ZERO
S_CLR1	PUSH	AF				; SAVE COUNTER
	SET	1,(IY+FLAGS3-Y)			; TO TELL CL_STR JUST TO DELETE
	CALL	CL_STR
	POP	AF
	INC	A
	CP	10H				; ALL STREAMS DONE?
        JR	C,S_CLR1
	JP	END1

;______________________________________________________________________

SV_1_2	LD	HL,DR+D_STR1			; FIRST OF THE EIGHT SYS. VARS.
	LD	DE,DR+D_STR2			; SECOND SET BASE ADDRESS
	LD	B,08H				; THERE ARE 8 SYS. VARS. TO BE SWITCHED
SVA	LD	A,(DE)				; FETCH ONE
	LD	C,(HL)				; AND ANOTHER
	LD	(HL),A				; STORE ONE
	LD	A,C
	LD	(DE),A				; AND ANOTHER
	INC	HL
	INC	DE				; STEP ON
	DJNZ	SVA				; ROUND FOR 8 PASSES
	RET

;______________________________________________________________________





COMM						; LOOK AT THE PRESENT CHARACTER
						; Z IS A COMMA OR A SEMI-COLON
	CP	2CH				; IS IT A COMMA?
	RET	Z
	CP	3BH				; IS IT A SEMI-COLON?
	RET

ST_END		; CHECK A STATEMENT IS ENDED
	CP	0DH				; CARRIAGE RETURN?
	JR	Z,ST_E1
	CP	3AH				; COLON?
	JR	NZ,SC_ERR

ST_E1	RST	SYNT
	RET	NZ				; IN RUN-TIME RETURN TO A RUN MODULE


END1		; RETURN AFTER RUN-TIME OR SYNTAX TIME
	LD	SP,(ERR_SP)			; DROP THE MACHINE STACK
	LD	(IY+ERR_NR-Y),0FFH		; NO ERROR AFTER ALL
	LD	HL,1BF4H			; RETURN ADDRESS IN SYNTAX TIME
	RST	SYNT
	JR	Z,ENDSYN			; JUMP FORWARD IF CHECKING SYNTAX

	LD	A,7FH				; ELSE CHECK THE BREAK KEY
	IN	A,(0FEH)
	RRA
	JR	C,END2
	LD	A,0FEH
	IN	A,(0FEH)
	RRA
	JR	NC,BREAKL			; JUMP IF BREAK PRESSED

END2	LD	HL,1B7DH			; RETURN ADDRESS IN RUN-TIME
ENDSYN	PUSH	HL
	RST	R_BASE				; EXIT


BREAKL	LD	(IY+ERR_NR-Y),14H
	RST	ROMERR				; 'BREAK INTO PROGRAM'


EXP	RST	CALBAS
	DEFW	1C8CH				; FETCH A STRING EXPRESSION
	RST	SYNT
	RET	Z				; BACK IN SYNTAX TIME
	PUSH	AF
	RST	CALBAS
	DEFW	2BF1H				; POPSTR
	POP	AF
	RET					; BC - LENGTH,	DE - START


N_LETT		; NEXT LETTER
	RST	CALBAS
	DEFW	0020H				; STEP ON

LETT		; LETTER
	CALL	EXP
	JR	Z,LETT_N			; JUMP FORWARD IN SYNTAX TIME  - FOR A NUMBER
	PUSH	AF				; SAVE FLAG
	LD	A,C
	DEC	A
	OR	B
	JR	NZ,IDE_ER			; LENGTH MUST BE '1'
	LD	A,(DE)				; FETCH CODE
	RST	CALBAS
	DEFW	2C8DH				; IS IT A LETTER
	JR	NC,IDE_ER
	AND	0DFH				; FORCE IT TO UPPER CASE
	LD	(DR+L_STR1),A			; YES - SO KEEP IT
	POP	AF				; FLAG BACK
LETT_N	CP	0DH				; END OF LINE?
	RET	Z
	CP	3AH				; END OF STATEMENT?
	RET	Z
	CP	0A5H				; NO NUMBER IF A TOKEN IS FOUND
        RET	NC
		; THEREFORE A NUMBER TO FOLLOW
	CALL	COMM				; LOOK FOR THE SEPARATOR
	JP	NZ,END_ER
	RST	CALBAS
	DEFW	0020H				; STEP ON
NUMB_	RST	CALBAS
	DEFW	1C82H				; FIND A NUMERICAL EXPRESSION
	RST	SYNT
	RET	Z				; RETURN IN SYNTAX TIME
	PUSH	AF
	RST	CALBAS
	DEFW	1E99H				; NUMBER TO BC
	LD	(DR+D_STR1),BC			; STORE NUMBER
	POP	AF
	RET					; FINISHED


IDE_ER	RST	SH_ERR
	DEFB	2				; 'INVALID DEVICE EXPRESSION'


N_NAME		; FIND THE NEXT NAME
	RST	CALBAS
	DEFW	0020H				; STEP ON
	CALL	EXP				; FETCH A STRING EXPRESSION
	RET	Z
	PUSH	AF
	LD	A,C
	OR	B
	JR	Z,IN_ER				; MUST NOT BE NULL
	LD	HL,000AH
	SBC	HL,BC
	JR	C,IN_ER				; MUST NOT BE OVER TEN LETTERS
	LD	(DR+N_STR1),BC			; SAVE LENGTH
	LD	(DR+N_STR1+2),DE		; SAVE START
	POP	AF
	RET

IN_ER	RST	SH_ERR
	DEFB	3				; 'INVALID NAME'

N_STR		; FIND THE NEXT STREAM NUMBER
	RST	CALBAS
	DEFW	0020H				; STEP ON
	RST	CALBAS
	DEFW	1C82H				; FIND A NUMBER
	RST	SYNT
	RET	Z				; BACK IN SYNTAX TIME
	PUSH	AF
	RST	CALBAS
	DEFW	1E94H				; NUMBER INTO 'A'
	CP	10H
	JR	NC,STR_ER
	LD	(DR+S_STR1),A			; STORE IT
	POP	AF
	RET

STR_ER	RST	SH_ERR
	DEFB	1				; 'INVALID STREAM NUMBER'


M_CHEK		; CHECK THE "M" & THE DRIVE NUMBER
	LD	A,(DR+L_STR1)			; FETCH THE LETTER
	CP	4DH				; IS IT "M" ?
	JP	NZ,IDE_ER
DR_CHK	LD	DE,(DR+D_STR1)			; FETCH THE NUMBER
	LD	A,E
	OR	D
	JR	Z,IDN_ER			; 'INVALID DRIVE NUMBER'
	INC	DE
	LD	A,E
	OR	D
	JR	Z,MDN_ER			; DRIVE NUMBER MUST NOT BE FFFFH
	DEC	DE
	LD	HL,0008H
	SBC	HL,DE
	RET	NC

IDN_ER	RST	SH_ERR
	DEFB	4				; 'INVALID DRIVE NUMBER'

MDN_ER	RST	SH_ERR
	DEFB	08H				; 'MISSING DRIVE NUMBER'


MNCHEK		; CHECK THE "M", A DRIVE NUMBER & A NAME ARE ALL GIVEN
	CALL	M_CHEK
	LD	A,(DR+N_STR1+1)			; FETCH THE HIGH LENGTH BYTE OF THE NAME
	AND	A
	RET	Z				; RETURN IF NAME GIVEN

MN_ER	RST	SH_ERR
	DEFB	6				; 'MISSING NAME'

N_CHEK		; CHECK THE STATION NUMBER
	LD	DE,(DR+D_STR1)			; FETCH THE NUMBER
	INC	DE
	LD	A,E
	OR	D
	JR	Z,MSN_ER			; NUMBER NOT TO BE FFFFH
	DEC	DE
	LD	HL,0040H
	SBC	HL,DE
	RET	NC				; STATION NUMBER RANGE IS 01H - 40H

ISN_ER	RST	SH_ERR
	DEFB	5				; 'INVALID STATION NUMBER'

MSN_ER	RST	SH_ERR
	DEFB	7				; 'MISSING STATION NUMBER'

NLETNN		; FIND THE NEXT LETTER, NUMBER (IF PRESENT) & NAME
	CALL	N_LETT				; LETTER & NUMBER (IF GIVEN)
	CALL	COMM				; THERE MUST BE A SEPARATOR
	JP	NZ,END_ER
	CALL	N_NAME				; FIND THE NAME
	RET

B_CHEK	LD	HL,(DR+D_STR1)			; FETCH NUMBER
	INC	HL
	LD	A,L
	OR	H
	RET	NZ				; RETURN IF NUMBER PRESENT

MBR_ER	RST	SH_ERR
	DEFB	9				; 'MISSING BAUD RATE'

ST_V_F		; ARE THE DETAILS A STREAM OR A FILE?
	RST	CALBAS
	DEFW	0020H				; STEP ON
	CP	23H				; A 'HASH'?
	JP	Z,N_STR				; IF SO - IT IS A STREAM

;		ELSE FIND THE FILE DETAILS
	CALL	LETT				; FIND LETTER & NUMBER (IF PRESENT)
	CALL	COMM				; IS THERE A SEPARATOR?
	JR	NZ,STVF1			; FORWARD IF NOT
	CALL	N_NAME				; FIND THE NEXT NAME
STVF1	RST	SYNT
	RET	Z				; BACK IN SYNTAX TIME
		; ST_V_F		RUN MODULE
	LD	A,(DR+L_STR1)			; FETCH THE LETTER
	CP	54H				; IS IT "T" ?
	RET	Z
	CP	42H				; IS IT "B" ?
	RET	Z
	CP	4EH				; IS IT "N" ?
	JP	Z,N_CHEK			; RETURN AFTER CHECKING STATION NUMBER SPECIFIED
	JP	MNCHEK				; FINALLY CHECK FOR "M",DRIVE NUMBER & NAME
